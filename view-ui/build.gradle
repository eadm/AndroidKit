apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'ru.nobird.android.view'
version = versions.viewUIName

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.viewUIName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation libraries.kotlin
    implementation libraries.appCompat
    implementation libraries.material
    implementation libraries.archViewModel
    api libraries.coreModel
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    repositories {
        maven {
            name = "GitHub"
            url = uri("https://maven.pkg.github.com/eadm/AndroidKit")
            credentials {
                username = System.getenv("GITHUB_USER") ?: project.properties["GITHUB_USER"]
                password = System.getenv("GITHUB_PERSONAL_ACCESS_TOKEN") ?: project.properties["GITHUB_PERSONAL_ACCESS_TOKEN"]
            }
        }
    }

    publications {
        ViewUI(MavenPublication) {
            groupId 'ru.nobird.android.view'
            artifactId 'ui'
            version versions.viewUIName
            artifact("$buildDir/outputs/aar/view-ui-release.aar")
            artifact(sourceJar)
        }
    }
}